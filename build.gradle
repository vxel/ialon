// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.3.1"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "org.sonarqube" version "3.5.0.2730"
}

sonarqube {
    properties {
        property "sonar.projectKey", "vxel_ialon"
        property "sonar.organization", "vxel"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.exclusions","**/protobuf/*.java,com/rvandoosselaer/**/*.java"
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def jme3 = [v: '3.6.0-stable', g: 'org.jmonkeyengine']
def sio2 = [v: '1.7.0', g: 'com.simsilica']

project(":core") {
    apply plugin: "java"
    dependencies {
        implementation "${jme3.g}:jme3-core:${jme3.v}"
        implementation "${jme3.g}:jme3-effects:${jme3.v}"
        implementation "${jme3.g}:jme3-jogg:${jme3.v}"
        implementation "${jme3.g}:jme3-plugins:${jme3.v}"
        implementation "${sio2.g}:sio2:${sio2.v}"
    }
}

project(":desktop") {
    apply plugin: "java"
    dependencies {
        implementation project(":core")
        implementation "${jme3.g}:jme3-desktop:${jme3.v}"
        implementation "${jme3.g}:jme3-lwjgl:${jme3.v}"
        implementation "${sio2.g}:sio2:${sio2.v}"
        testImplementation project(":core").sourceSets.main.resources
    }
}

project(":app") {
    apply plugin: "android"
    dependencies {
        implementation project(":core")
        implementation "${jme3.g}:jme3-android:${jme3.v}"
        implementation "${jme3.g}:jme3-android-native:${jme3.v}"
    }
}

