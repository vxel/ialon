// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        classpath "com.badlogicgames.gdx:gdx-tools:1.10.0"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def jme3 = [v: '3.4.0-stable', g: 'org.jmonkeyengine']

project(":core") {
    apply plugin: "java"
    dependencies {
        implementation "${jme3.g}:jme3-core:${jme3.v}"
        implementation "${jme3.g}:jme3-effects:${jme3.v}"
        implementation "${jme3.g}:jme3-jogg:${jme3.v}"
        implementation "${jme3.g}:jme3-plugins:${jme3.v}"
    }
}

project(":desktop") {
    apply plugin: "java"
    dependencies {
        implementation project(":core")
        implementation "${jme3.g}:jme3-desktop:${jme3.v}"
        implementation "${jme3.g}:jme3-lwjgl:${jme3.v}"
    }
}

project(":app") {
    apply plugin: "android"
    dependencies {
        implementation project(":core")
        implementation "${jme3.g}:jme3-android:${jme3.v}"
        implementation "${jme3.g}:jme3-android-native:${jme3.v}"
    }
}

// Store the parameters you want to pass the texturePacker here...
project.ext.texturePacker = [ "core/src/main/resources/ialon-theme/", "core/src/main/resources/ialon-theme/", "texture" ]

// Import the texture packer
import com.badlogic.gdx.tools.texturepacker.TexturePacker
import com.badlogic.gdx.utils.Json;

// Add a new task that packs the textures for you
task texturePacker {
    doLast {
        if (project.ext.has('texturePacker')) {
            logger.info "Calling TexturePacker: " + texturePacker
            TexturePacker.process(new Json().fromJson(com.badlogic.gdx.tools.texturepacker.TexturePacker.Settings.class, new FileReader("settings.texturepacker")),
                    texturePacker[0], texturePacker[1], texturePacker[2])
        }
    }
}
